Required:milk-tea-coffee.tsv

float[][] data;     
int[] years;       
float dataMin, dataMax; 
float plotX1, plotY1, plotX2, plotY2; 

void setup() { 
  size(720, 405); 
  smooth(); 
  String[] rows = loadStrings("milk-tea-coffee.tsv"); 
  String[] headers = split(rows[0], TAB); 
  int colCount = headers.length - 1;  
  int rowCount = rows.length - 1; 
  data = new float[rowCount][colCount]; 
  years = new int[rowCount]; 

  for (int i = 1; i < rows.length; i++) { 
    String[] pieces = split(rows[i], TAB); 
    years[i - 1] = int(pieces[0]); 
    for (int j = 1; j < pieces.length; j++) { 
      data[i - 1][j - 1] = float(pieces[j]); 
    } 
  } 

  dataMin = 0; 
  dataMax = getTableMax(); 
  plotX1 = 50; 
  plotX2 = width - plotX1; 
  plotY1 = 60; 
  plotY2 = height - plotY1; 
} 

void draw() { 
  background(224); 
  fill(255); 
  noStroke(); 
  rectMode(CORNERS); 
  rect(plotX1, plotY1, plotX2, plotY2); 
  strokeWeight(4); 
  stroke(#5679C1); 
  drawDataPoints(0); 
} 

void drawDataPoints(int col) { 
  int rowCount = years.length; 
  for (int row = 0; row < rowCount; row++) { 
    float value = data[row][col]; 
    float x = map(years[row], years[0], years[years.length - 1], plotX1, plotX2); 
    float y = map(value, dataMin, dataMax, plotY2, plotY1); 
    point(x, y); 
  } 
} 

float getTableMax() { 
  float maxVal = -Float.MAX_VALUE; 
  for (int i = 0; i < data.length; i++) { 
    for (int j = 0; j < data[i].length; j++) { 
      if (data[i][j] > maxVal) maxVal = data[i][j]; 
    } 
  } 
  return maxVal; 
}
